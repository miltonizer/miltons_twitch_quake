// Author: XantoM (#tvs)

set tvs_tp_version "3.2"
set tvs_tp_date "2021-10-06"
alias .tvs_version "say_team {tp cfg $tvs_tp_version} ($tvs_tp_date)"

////////////////////////////////////////////////////////////////////////////////////////////////////
//                                                                                                //
//                                         Variables                                              //
//                                                                                                //
////////////////////////////////////////////////////////////////////////////////////////////////////

// Names
tp_name_rl				"{&cf60rl&cfff}"
tp_name_lg				"{&c399lg&cfff}"
tp_name_ga				"{&c0b0ga&cfff}"
tp_name_ya				"{&cff0ya&cfff}"
tp_name_ra				"{&cf00ra&cfff}"
tp_name_armor			"armor"
tp_name_armortype_ga	"g"
tp_name_armortype_ya	"y"
tp_name_armortype_ra	"r"
tp_name_quad			"{&c05fquad&cfff}"
tp_name_pent			"{&cf00pent&cfff}"
tp_name_ring			"{&cff0ring&cfff}"
tp_name_mh				"{&c0a0mega&cfff}"
tp_name_backpack		"{&cf39pack&cfff}"
tp_name_enemy			"{&cf00enemy&cfff}"
tp_name_quaded			"quaded"
tp_name_pented			"pented"
tp_name_eyes			"eyes"
tp_name_someplace		" "
tp_name_at				"at"
tp_name_separator		"$x20"


// Need
tp_need_weapon			"87"
tp_need_health			"50"
tp_need_ga				"70"
tp_need_ya				"70"
tp_need_ra				"70"
tp_need_shells			"0"
tp_need_nails			"0"
tp_need_cells			"15"
tp_need_rockets      "7"

// Vars

set_tp tvsc_need			"&cf66"
set_tp tvsc_white 			"&cfff"
set_tp tvsc_help			"&cff0"
set_tp tvsc_lost			"&cf00"
set_tp tvsc_safe			"&c0f0"
set_tp tvsc_pack			"&cf39"
set_tp tvsc_coming			"&cf60"

set_tp tvs_space			"$x20"
set_tp tvs_space3			"$x20$x20$x20"

set_tp tvs_loc				"$[{%l}$]"
set_tp tvs_colorloc			"$[%l$]"
set_tp tvs_deathloc			"$[%d$]"
set_tp tvs_pointloc			"$[{%y}$]"
set_tp tvs_tookloc			"$[{%Y}$]"

set_tp tvs_str_health		"%h"
set_tp tvs_str_armor		"%A%a"
set_tp tvs_str_weapon		""
set_tp tvs_str_cells		""
set_tp tvs_str_rockets		""
set_tp tvs_str_rl			""
set_tp tvs_str_lg			""


////////////////////////////////////////////////////////////////////////////////////////////////////
//                                                                                                //
//                                         Aliases                                                //
//                                                                                                //
////////////////////////////////////////////////////////////////////////////////////////////////////

alias __do_nothing          ""

alias __say_team			"if ($health < 1) then __dead %1 else __alive %1"

alias __dead				"if ('$matchtype' == '2on2') then .lost else if ('$weapon' == '$tp_name_lg') then .left_pack lg- else if ('$weapon' == '$tp_name_rl') then .left_pack rl- else .lost"
alias __alive				"if ('%1' isin 'report safe sync') then __check_gear_%1; wait; .%1"

alias __check_gear_report	"__set_weapon_string; __set_health_string; __set_armor_string"
alias __check_gear_safe		"__set_weapon_string"
alias __check_gear_sync		"__set_weapon_string"

alias __set_health_string	"if ('$bestweapon' isin '$tp_name_lg $tp_name_rl' AND 'health' isin '$need') then __set_need tvs_str_health %h else if ($health > 110 OR $bestweapon isin '$tp_name_lg $tp_name_rl' OR '$powerups' != '') then __set_white tvs_str_health %h else set_tp tvs_str_health %h"

alias __set_armor_string	"if ('$bestweapon' isin '$tp_name_lg $tp_name_rl' AND 'armor' isin '$need') then __set_need tvs_str_armor %a else if ($armor > 0 OR $bestweapon isin '$tp_name_lg $tp_name_rl') then __set_white tvs_str_armor %a else set_tp tvs_str_armor %a; __prepend tvs_str_armor %A"

alias __set_weapon_string	"__reset tvs_str_weapon; if ('$tp_name_rl' isin '$weapons' AND '$tp_name_lg' isin '$weapons') then __has_rlg else if ('$bestweapon' isin '$tp_name_rl') then __has_rl else if ('$bestweapon' isin '$tp_name_lg') then __has_lg else __has_shit"

alias __set_rockets_string	"__reset tvs_str_rockets; if ('$tp_name_rl' isin '$weapons' AND '$tp_name_rockets' isin '$need') then __set_need tvs_str_rockets :$rockets"
alias __set_cells_string	"if ('$tp_name_lg' isin '$weapons' AND $cells < 10) then __set_need tvs_str_cells $cells else set_tp tvs_str_cells $cells"

alias __check_loot			"if ($cells > 10 AND '$powerups' = '') then __append tvs_str_weapon $tvs_spacec:$cells else if ('$bestweapon' = 'gl') then __append tvs_str_weapon :$rockets"

alias __has_rlg				"__has_rl; __append tvs_str_weapon $tvs_space; __has_lg"
alias __has_rl				"set_tp tvs_str_rl $tp_name_rl; __set_rockets_string; __append tvs_str_weapon $tvs_str_rl$tvs_str_rockets"
alias __has_lg				"set_tp tvs_str_lg $tp_name_lg; __set_cells_string; __append tvs_str_weapon $tvs_str_lg:$tvs_str_cells"
alias __has_shit			"set_tp tvs_str_weapon $bestweapon; __check_loot"

alias __set_white			"set_tp %1 {%2}"
alias __set_need			"set_tp %1 {$tvsc_need%2$tvsc_white}"
alias __append				"set_tp %1 $%1%2"
alias __prepend				"set_tp %1 %2$%1"
alias __reset				"set_tp %1 $qt$qt"

alias __safe            	"__say_team safe"
alias __report          	"__say_team report"
alias __help	          	"__say_team help"
alias __coming				"__say_team coming"
alias __waiting				"__say_team waiting"
alias __sync				"if ($bestweapon isin '$tp_name_sg') then .attack 		else __say_team sync"
alias __replace				"__say_team replace"

alias __get_quad			"if ('$tp_name_quad' isin '$powerups') then __team_powerups 		else .get $tp_name_quad"
alias __get_ring			"if ('$tp_name_ring' isin '$powerups') then __team_powerups       else .get $tp_name_ring"
alias __get_pent			"if ('$tp_name_pent' isin '$powerups') then __team_powerups 		else .get $tp_name_pent"
alias __quad_over			"if ('$tp_name_quad' isin '$powerups') then __team_powerups 		else .quad_over"

alias __shownick			"if_exists alias shownick shownick __do_nothing"
alias __point	       		"__shownick; if ('$point' != '$tp_name_nothing' AND '$ledpoint' != '$G') then .point"
alias __need				"if ($health < 1) then __dead else if ('$need' = '$tp_name_nothing') then __report else .need"
alias __took            	"if ('$took' = '$tp_name_nothing' OR ('$powerups' != '' AND '$took' = '$tp_name_rockets') OR '$took' isin '$tp_name_ring $tp_name_pent $tp_name_quad') then __team_powerups else .took"
alias __you_take             "if $ledpoint = $G then .take %x else .take u"
alias __team_powerups		"if ('$powerups' = '') then __do_nothing else if ('$tp_name_ring' isin '$powerups') then .team_powerups_ring else if ( $health < 1 AND '$tp_name_quad' isin '$powerups' ) then .quad_over else .team_powerups"

alias __get_my_stuff		"__weapon_switch; if ($health < 1) then __dead else if ($bestweapon isin '$tp_name_rl $tp_name_lg') then .get_my $bestweapon else if ($cells > 0) then .get_my cells:$cells else if (('$mapname' = 'e1m2' or '$mapname' = 'dm2') and $rockets > 0) then .get_my rockets:$rockets"
alias __weapon_switch		"impulse 7 8 6 5 3 2"

alias __trick				"if ($health < 1) then __dead		else if ('rl' isin '$weapons') then __trick_rl		else __map_trick"
alias __trick_rl         	"if ($armor > 40 or $health > 50 or 'pent' isin '$powerups') then __map_trick 		else __get_my_stuff"
alias __map_trick       	"if ('$mapname' isin 'dm2 dm3') then __map_trick_$mapname 		else .trick"
alias __map_trick_dm2       "if ('big' isin '$location') then .quad_jump	else if ('quad' isin '$location') then .quad_jump	else if ('high' isin '$location') then .quad_jump	else if ('water' isin '$location') then .quad_jump else if ('ng' isin '$location') then .quad_jump	else if ('secret' isin '$location') then .quad_jump else if ('tele' isin '$location') then .slipped		else if ('button' isin '$location') then .open else __sync"
alias __map_trick_dm3       "if ('water' isin '$location') then .discharge		else if ('quad' isin '$location') then .delay		else if ('pent' isin '$location') then .delay		else if ('lift' isin '$location') then .delay		else __sync"

alias __itemsoon			".item_soon"
alias __left_pack			".left_pack"
alias __enemy_powerups	".enemy_powerups"
alias __enemy_rl			".enemy_rl"
alias __enemy_rl_dead	".enemy_rl_dead"
alias __ask_for_status	".ask_for_status"
alias __lost				".lost"

////////////////////////////////////////////////////////////////////////////////////////////////////
//                                                                                                //
//                               Positions				                         				  //
//                                                                                                //
////////////////////////////////////////////////////////////////////////////////////////////////////

alias __enemy_high			"if $mapname = dm3 then set tvs_enemy_pos lift		else if $mapname = dm2 then set tvs_enemy_pos high	else if $mapname = e1m2 then set tvs_enemy_pos ya; .enemy_at_pos"
alias __enemy_low			"if $mapname = dm3 then set tvs_enemy_pos ra-tunnel	else if $mapname = dm2 then set tvs_enemy_pos ra/mh	else if $mapname = e1m2 then set tvs_enemy_pos rl; .enemy_at_pos"
alias __enemy_pos1       	"if $mapname = dm3 then set tvs_enemy_pos ra		else if $mapname = dm2 then set tvs_enemy_pos tele	else if $mapname = e1m2 then set tvs_enemy_pos ya	else if $mapname = cmt3 then set tvs_enemy_pos ra else if $mapname = cmt4 then set tvs_enemy_pos ya; .enemy_at_pos"
alias __enemy_pos2       	"if $mapname = dm3 then set tvs_enemy_pos sng		else if $mapname = dm2 then set tvs_enemy_pos secret	else if $mapname = e1m2 then set tvs_enemy_pos gl	else if $mapname = cmt3 then set tvs_enemy_pos ya else if $mapname = cmt4 then set tvs_enemy_pos ra; .enemy_at_pos"
alias __enemy_pos3      	 "if $mapname = dm3 then set tvs_enemy_pos ra-tunnel	else if $mapname = dm2 then set tvs_enemy_pos low	else if $mapname = e1m2 then set tvs_enemy_pos mega	else if $mapname = cmt3 then set tvs_enemy_pos rl else if $mapname = cmt4 then set tvs_enemy_pos lg; .enemy_at_pos"
alias __enemy_pos4       	"if $mapname = dm3 then set tvs_enemy_pos ya		else if $mapname = dm2 then set tvs_enemy_pos big	else if $mapname = e1m2 then set tvs_enemy_pos rl	else if $mapname = cmt3 then set tvs_enemy_pos lg else if $mapname = cmt4 then set tvs_enemy_pos pent; .enemy_at_pos"


////////////////////////////////////////////////////////////////////////////////////////////////////
//                                                                                                //
//                                         Output                                                 //
//                                                                                                //
////////////////////////////////////////////////////////////////////////////////////////////////////

alias .attack			   	"say_team $\ $R$R attack $tvs_deathloc %E"
alias .cancel           	"say_team $\ $tvs_nick_color cancel that / can't"
alias .coming           	"say_team $\ {$tvsc_coming$tvs_nick_text coming $tvs_colorloc$tvsc_white} $powerups"
alias .delay            	"say_team $\ $tvs_nick_color delay $tvs_loc"
alias .discharge            "say_team $\ $tvs_nick_color $Y$Y discharge $tvs_loc"
alias .enemy_powerups   	"say_team $\ $R$R $tp_name_enemy %q"
alias .enemy_rl				"say_team $\ $R$R enemy rl at $tvs_pointloc"
alias .enemy_rl_dead       	"say_team $\ $tvs_nick_color rl dead at $tvs_loc"
alias .enemy_at_pos         "say_team $\ $R$R enemy at $[{$tvs_enemy_pos}$]"
alias .get          		"say_team $\ $tvs_nick_color get %1 %2 %3"
alias .get_my          		"say_team $\ $tvs_nick_color get my %1 $tvs_loc"
alias .help             	"say_team $\{$tvsc_help $tvs_nick_text help $tvs_colorloc %E} $powerups"
alias .item_soon           	"say_team $\ $tvs_nick_color item soon $tvs_loc"
alias .lost             	"say_team $\{$tvsc_lost $tvs_nick_text lost $tvs_deathloc %E}"
alias .need					"say_team $\ $tvs_nick_color need %u $tvs_loc $powerups"
alias .open             	"say_team $\ $tvs_nick_color open $tvs_loc"
alias .left_pack			"say_team $\{$tvsc_pack $tvs_nick_text %1pack left at $tvs_deathloc %E}"
alias .point            	"say_team $\ $tvs_nick_color $x89$x89 %x at $tvs_pointloc"
alias .quad_over        	"say_team $\ $tvs_nick_color $tp_name_quad over"
alias .quad_jump        	"say_team $\ $tvs_nick_color quad-jump"
alias .replace          	"say_team $\ $tvs_nick_color replace $tvs_loc $powerups"
alias .report				"say_team $\ $tvs_nick_color $tvs_loc $tvs_str_armor/$tvs_str_health $tvs_str_weapon $powerups"
alias .safe					"say_team $\{$tvsc_safe $tvs_nick_text safe $tvs_colorloc$tvsc_white} $tvs_str_weapon $powerups"
alias .slipped				"say_team $\ $tvs_nick_color $tp_name_enemy slipped $[{tele}$]"
alias .ask_for_status		"say_team $\ $tvs_nick_color report status! $powerups"
alias .sync					"say_team $\ $tvs_nick_color sync $tvs_loc $tvs_str_weapon $powerups"
alias .take             	"say_team $\ $tvs_nick_color %1 take $tvs_loc"
alias .team_powerups    	"say_team $\ $tvs_nick_color team %p"
alias .team_powerups_ring 	"say_team $\ $tvs_nick_color $Y$R$G$B team %p"
alias .took	       			"say_team $\ $tvs_nick_color took $took $tvs_tookloc"
alias .trick            	"say_team $\ $tvs_nick_color trick $tvs_loc"
alias .waiting				"say_team $\ $tvs_nick_color waiting at $tvs_loc $powerups"

// demo of output
alias tp_demo_01 ".attack; .coming; .delay; .discharge; .enemy_powerups; .enemy_rl; .enemy_rl_dead; .enemy_at_pos; .get;"
alias tp_demo_02 ".get_my; .help; .item_soon; .lost; .need; .open; .left_pack; .point; .quad_over; "
alias tp_demo_03 ".quad_jump; .replace; .report; .safe; .slipped; .ask_for_status; .sync; .take;"
alias tp_demo_04 ".team_powerups; .team_powerups_ring; .took; .trick; .waiting"

////////////////////////////////////////////////////////////////////////////////////////////////////
//                                                                                                //
//                               T E A M P L A Y   C O M M A N D S                                //
//                                                                                                //
////////////////////////////////////////////////////////////////////////////////////////////////////

tp_pickup    powerups armor mh ssg sng gl rl lg pack rockets cells
tp_took      powerups armor mh ssg sng gl rl lg pack rockets cells 
tp_point     powerups armor players mh ssg sng gl rl lg sng pack rockets cells

filter       clear